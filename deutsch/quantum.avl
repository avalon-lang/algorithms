-[
Quantum solution to Deutsch's problem.

- author     ntwali bashige
- copyright  2018 (c) ntwali bashige
- license    MIT license
]-

import io
import quant
import oracles.quantum


def __main__ = (val args : [string]) -> void:
    val input = 0q0, ancilla = 0q1

    -- step 1 : apply the Hadamard gate to both the input and the ancilla
    Quant.had(ref input)
    Quant.had(ref ancilla)

    -- step 2 : call the oracle
    Oracle.f0(ref input, ref ancilla)

    -- step 3 : apply the Hadamard gate to the input
    Quant.had(ref input)

    -- step 4 : measure the input
    val answer = cast(ref input) -> bit

    -- print the result
    if answer == 0b0:
        Io.println("The oracle contains a constant function.")
    else:
        Io.println("The oracle contains a balanced function.")

    -- we are done
    return
